#!/usr/local/bin/gnuplot

# File: fred_get_bbox

if (!exists("force_xmin")) {
  force_xmin = 0
}
if (!exists("force_xmax")) {
  force_xmax = 0
}
if (!exists("force_ymin")) {
  force_ymin = 0
}
if (!exists("force_ymax")) {
  force_ymax = 0
}
if (!exists("shapefiles")) {
   print "ERROR in fred_make_box: no shapefiles defined"
   exit
}
print sprintf("shapefiles = |%s|", shapefiles)

unset key; unset tics;

# create a fake plot in order to gather the x and y bbox
set term unknown
plot for [n=1:words(shapefiles)] word(shapefiles,n) u ($2):($1) ls 99 w filledcurve notitle

# initialize the new x and y boundaries
xmin = GPVAL_DATA_X_MIN
xmax = GPVAL_DATA_X_MAX
ymin = GPVAL_DATA_Y_MIN
ymax = GPVAL_DATA_Y_MAX
# print xmin, xmax, ymin, ymax

# find the x and y bbox in the data
xrng = xmax - xmin
yrng = ymax - ymin

# add some space on each side
xmin = xmin - 0.075*xrng
xmax = xmax + 0.075*xrng
ymin = ymin - 0.025*yrng
ymax = ymax + 0.025*yrng
print xmin, xmax, ymin, ymax

if (force_xmin) { xmin = force_xmin }
if (force_xmax) { xmax = force_xmax }
if (force_ymin) { ymin = force_ymin }
if (force_ymax) { ymax = force_ymax }
# print xmin, xmax, ymin, ymax

# earth geometry
km_per_deg_lat = 111.325
mean_lat = (ymax + ymin) / 2.0
km_per_deg_lon = cos(mean_lat * pi / 180.0) * km_per_deg_lat
lat_lon_ratio = 0.9 * km_per_deg_lon / km_per_deg_lat

# print sprintf("mean latitude = %0.5f", mean_lat)
# print sprintf("km_per_deg_lat = %0.5f", km_per_deg_lat)
# print sprintf("km_per_deg_lon = %0.5f", km_per_deg_lon)
# print sprintf("lat_lon_ratio = %0.5f", lat_lon_ratio)

do for [i=1:5] {
  # find the x and y bbox
  xrng = xmax - xmin
  yrng = ymax - ymin
  
  # adjust the x range if too small
  xtarg = yrng / lat_lon_ratio
  xxtra = (xrng < xtarg)? (xtarg - xrng)*0.5 : 0
  xmin = xmin - xxtra
  xmax = xmax + xxtra
  
  # adjust the y range if too small
  ytarg = lat_lon_ratio * xrng
  yxtra = (yrng < ytarg)? (ytarg - yrng)*0.5 : 0
  ymin = ymin - yxtra
  ymax = ymax + yxtra
  # print xmin, xmax, ymin, ymax
}

# save the bbox to a file
print "writing bbox to  BBOX"
print xmin, xmax, ymin, ymax
set print "BBOX"
print sprintf("ymin = %f",ymin)
print sprintf("xmin = %f",xmin)
print sprintf("ymax = %f",ymax)
print sprintf("xmax = %f",xmax)
set print

